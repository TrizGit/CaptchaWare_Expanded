extends Microgame

const PHONE_CALL_WINDOW = preload("uid://bnyk2f5trja0r")

@onready var camera = get_tree().get_first_node_in_group("camera")
@onready var phonenumber_label: Label = $phonenumber

var phone_number: int = 0

var real_number_calling : bool = false
var has_answered: bool = false

var number_of_calls : int = 0
var how_many_fake_calls : int = 1
const DIFFICULTY_ARRAY = [2.5, 2.0, 1.5, 1.0]
var phonenumber_order : Array = []
var	phonenumber_cur_order : String = 'asd'

var fake_number_type : PackedStringArray = [
	"ding",
	"erynn",
	"hira",
	"jackson",
	"jam",
	"juhin",
	"julnz",
	"marcz",
	"miel",
	"mystery",
	"nonsense",
	"puppet",
	"zac",
	"lopil",
	"jemi"
]

@onready var popup: AudioStreamPlayer = $popup

func _ready() -> void:
	phonenumber_label.text = generate_number()
	how_many_fake_calls = randi_range(difficulty, difficulty + 2)
	for i in range(how_many_fake_calls):
		phonenumber_order.append('fake')
	phonenumber_order.append('real')
	phonenumber_order.shuffle()

func pop_up_window() -> void: #spawn window	
	var phone_call_window : Control = PHONE_CALL_WINDOW.instantiate()
	var ring_time : Timer = phone_call_window.get_node("ring_time")
	ring_time.wait_time = DIFFICULTY_ARRAY[difficulty - 1]
	
	phone_call_window.call_answered.connect(on_call_answered)
	phone_call_window.call_declined.connect(on_call_declined)
	phonenumber_cur_order = phonenumber_order[number_of_calls]
	print_debug("Ring Time: ", ring_time.wait_time, "\nNumber of Calls: ", number_of_calls, "\nRing Order: ", phonenumber_order)
	
	add_child(phone_call_window)

	set_camera_shake.emit(10, 0.5)
	popup.play()

	if phonenumber_cur_order == 'real':
		phone_call_window.phone_number_node.text = phonenumber_label.text
		real_number_calling = true
	else:
		phone_call_window.phone_number_node.text = generate_number()
		phone_call_window.phone_audio.stream = get_phone_call_audio()
	number_of_calls += 1

func get_phone_call_audio() -> AudioStream:
	var path : String = "res://sounds/microgames/phone_verification/"

	if real_number_calling:
		path += "automated_message.mp3"
	else:
		path += "fake_numbers/" + fake_number_type[randi_range(0, fake_number_type.size() - 1)]
		var audio_phone_final : PackedStringArray = get_file_list(path, ".mp3")
		path += "/" + audio_phone_final[randi_range(0, audio_phone_final.size() - 1)]
	
	return load(path)

func generate_number() -> String:
	var number_text: String = ""
	phone_number = randi_range(9999999999, 1000000000)
	
	var number_array : PackedStringArray = str(phone_number).split()
	
	number_text = "+1 ("
	for i in range(10):
		number_text += number_array[i] + get_phone_format(i)
	return number_text

func get_phone_format(i: int) -> String:
	match i:
		2:
			return ") "
		5:
			return "-"
		_:
			return ""

func canSkip() -> bool:
	return false

func isWinning() -> bool:
	return has_answered && real_number_calling

func on_call_answered() -> void:
	has_answered = true
	if phonenumber_cur_order == 'real':
		await get_tree().create_timer(0.5).timeout
		force_end_mircogame()
	else: 
		await get_tree().create_timer(randf_range(1.5, 2.5)).timeout
		pop_up_window()

func on_transition_complete() -> void:
	var cam_tween := create_tween()
	cam_tween.tween_property(camera, "zoom", Vector2.ONE * 1.80, 4.0).set_ease(Tween.EASE_IN).set_trans(Tween.TRANS_CIRC)	

	await get_tree().create_timer(randf_range(2.0, 4.0)).timeout

	cam_tween.stop()
	cam_tween = create_tween()
	
	cam_tween.tween_property(camera, "zoom", Vector2.ONE * 1.43, 0.2).set_ease(Tween.EASE_OUT).set_trans(Tween.TRANS_EXPO).from(Vector2.ONE * 2)
	pop_up_window()

func on_call_declined() -> void:
	if number_of_calls >= len(phonenumber_order):
		await get_tree().create_timer(1.5).timeout
		force_end_mircogame()
		return
	
	await get_tree().create_timer(randf_range(1.5, 2.5)).timeout

	pop_up_window()
